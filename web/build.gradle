apply plugin: 'war'

configurations {
    webLibraries
}

repositories {
    ivy {
        url 'http://cdn.sencha.com/'
        layout 'pattern', {
            artifact "ext/gpl/ext-[revision]-gpl.[ext]"
        }
    }
}

dependencies {
    compile project(':services')
    compile "org.springframework:spring-webmvc:${springVersion}"
    compile 'org.codehaus.jackson:jackson-mapper-lgpl:1.9.13'

    runtime 'log4j:log4j:1.2.17'

    providedCompile 'javax.servlet:servlet-api:2.5'

    webLibraries group: 'com.sencha', name:'extjs', version: '4.2.1', ext: 'zip'
}

ext.unpackedWebLibraries = fileTree("$buildDir/unpackedWebLibraries") { builtBy 'unpackExtJs' }
task unpackExtJs(type: Sync) {
    FileTree extJsArchive = zipTree(configurations.webLibraries.files{it.name == 'extjs'}.iterator().next())

    into unpackedWebLibraries.dir
    from extJsArchive
    include 'ext-*/*.js'
    include 'ext-*/resources/ext-theme-neptune/'
    include 'ext-*/resources/css/ext-all-neptune*.css'
    eachFile{ if (relativePath.segments.length) { relativePath.segments[0] = 'extjs' } }
}

// Allow us to deploy directly from Gradle using Jetty
apply plugin: 'jetty'
apply from: rootProject.file('buildSrc/customJettyConfig.gradle')
jettyRun {
    dependsOn unpackedWebLibraries
    contextPath = ''
    resourceDirs << unpackedWebLibraries.dir
}
